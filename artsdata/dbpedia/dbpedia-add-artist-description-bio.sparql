PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX prov: <http://www.w3.org/ns/prov#>


insert {
    graph <http://kg.artsdata.ca/core> {
        << ?person schema:description ?c_en >> prov:wasDerivedFrom [ prov:wasDerivedFrom ?sameAs ;
                                                                   prov:hadPrimarySource <http://kg.artsdata.ca/resource/K1-18> ;
                                                                   prov:generatedAtTime ?stamp ] .
    }
}
where {
    bind (now() as ?stamp )
    {
        select  ?person ?sameAs_http ?sameAs where {
            ?person a schema:Person .
            filter(contains(str(?person),"kg.artsdata.ca/resource"))
            ?person schema:sameAs ?sameAs .
            filter(contains(str(?sameAs),"en.wikipedia.org"))
            bind(uri(concat("http:", strafter(str(?sameAs),"https:"))) as ?sameAs_http)
        } 
    }
    service <https://dbpedia.org/sparql> {
        ?sameAs_http foaf:primaryTopic ?a .
        ?a dbo:abstract ?c .
        filter(lang(?c) = "en")
       
    } 
     bind(strlang(?c,"en") as ?c_en)
    filter(bound(?c_en))
}