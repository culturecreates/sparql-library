PREFIX schema: <http://schema.org/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    ?event a schema:Event ;
           schema:name ?name ;
           schema:startDate ?startDate ;
           schema:description ?finalDesc ;
           schema:location ?loc ;
           schema:image ?image;
           schema:performer ?performer ;
           schema:endDate ?endDate .
    ?loc a schema:Place ;
         schema:name ?locName ;
         schema:address ?address .
    ?address a schema:PostalAddress ;
             schema:streetAddress ?streetAddress ;
             schema:addressRegion ?addressRegion ;
             schema:addressCountry ?addressCountry ;
             schema:postalCode ?postalCode ;
             schema:addressLocality ?addressLocality .
    ?performer a ?performerType ;
               schema:name ?performerName ; 
               schema:logo ?performerLogo ;
               schema:image ?performerImage .
}
WHERE {
    GRAPH <http://kg.artsdata.ca/culture-creates/artsdata-planet-footlight/nac-cnav2> {
        ?event a schema:Event ;
        schema:name ?name ;
        schema:startDate ?startDate ;
        schema:location ?loc ;
        schema:endDate ?endDate ;
    
        OPTIONAL { 
            ?event schema:description ?eventDesc ;
                   schema:image ?eventImage ;
                   schema:performer ?eventPerformer ;
            
        }

        OPTIONAL { 
            ?eventSeries a schema:EventSeries ;
            schema:subEvent ?event;
            schema:description ?seriesDesc ;
            schema:image ?seriesImage ;
            schema:performer ?seriesPerformer;
        }

        BIND(COALESCE(?eventDesc, ?seriesDesc) AS ?desc)
        BIND(COALESCE(?eventImage, ?seriesImage) AS ?image)
        BIND(COALESCE(?eventPerformer, ?seriesPerformer) AS ?performer)
        OPTIONAL {
            ?performer a ?performerType ;
            schema:name ?performerName ;
            schema:image ?performerImage .
        }
        
        BIND(CONCAT(
            STR(?desc), 
            " <div><a href=\"", STR(?image), "\" target=\"_blank\" rel=\"noopener noreferrer\">See image</a></div>",
            " <div><a href=\"", STR(?performerImage), "\" target=\"_blank\" rel=\"noopener noreferrer\">", "Performer: ",STR(?performerName), "</a></div>"
        ) AS ?finalDesc)

        OPTIONAL {
            ?loc a schema:Place ;
                 schema:name ?locName ;
                 schema:address ?address .

			OPTIONAL {
                ?address a schema:PostalAddress ;
                schema:streetAddress ?streetAddress ;
                schema:postalCode ?postalCode .
            }
        }
    }
}