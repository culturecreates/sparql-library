PREFIX schema: <http://schema.org/>
# select * 
delete {
    graph ?g {
        ?p_blank  ?p_p ?p_o  .
        ?x_s ?x_p ?p_blank .
    }
}
insert {
    graph ?g {
        ?p ?p_p ?p_o  .
        ?x_s ?x_p ?p .
    }
}
where {
    values ?g {<http://kg.artsdata.ca/culture-creates/huginn/rqd>}
    graph ?g {
        ?p a schema:Person ;
           schema:name ?name .
   
        ?p_blank a schema:Person;
                 ?p_p ?p_o ;
                 schema:name ?contact_point_name .
        ?x_s ?x_p ?p_blank .
    }
    filter (?name =  ?contact_point_name )
    filter(isBlank(?p_blank))
    filter(!isBlank(?p))
}

# Used to link People to their roles in Orgs for RDQ website by replacing blank nodes with person URIs