PREFIX schema: <http://schema.org/>
#select * 
delete {
    graph <http://kg.artsdata.ca/kim-davis/LDS_RQD/quebecdanse.org_rqd-orgs> {
        ?p_blank  ?p_p ?p_o  .
        ?x_s ?x_p ?p_blank .
    }
}
insert {
    graph <http://kg.artsdata.ca/kim-davis/LDS_RQD/quebecdanse.org_rqd-orgs> {
        ?p ?p_p ?p_o  .
        ?x_s ?x_p ?p .
    }
}
where {
    graph <http://kg.artsdata.ca/kim-davis/LDS_RQD/quebecdanse.org_rqd-people> {
        ?p a schema:Person ;
           schema:name ?name
    }    
    graph <http://kg.artsdata.ca/kim-davis/LDS_RQD/quebecdanse.org_rqd-orgs> {
        ?p_blank a schema:Person;
                 ?p_p ?p_o ;
                 schema:name ?contact_point_name .
        ?x_s ?x_p ?p_blank .
    }
    filter (?name =  ?contact_point_name )
    filter(isBlank(?p_blank))
}

# Used to link People to their roles in Orgs for RDQ website by replacing blank nodes with person URIs