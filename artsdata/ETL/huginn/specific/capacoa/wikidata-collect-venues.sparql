PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX schema: <http://schema.org/>
insert {
    graph <http://wikidata.org/capacoa-members> {
        ?org schema:location ?role .
        ?role schema:roleName "item operated P121" ;
              schema:location   ?itemOperated .
        ?itemOperated schema:geoCoordinates ?geo ;
                      schema:address ?street_address_sample ;
                      schema:additionalType ?itemOperatedTypeLabelConcat ;
                      schema:name ?itemOperatedLabel ;
                      schema:image ?image_sample ;
                      schema:containsPlace ?parts .
    }
}
where {
    SERVICE <https://query.wikidata.org/sparql> {
        SELECT ?org ?orgLabel ?itemOperatedLabel ?geo  
        (sample(?street_address) as ?street_address_sample) 
        (sample(?image) as ?image_sample) 
        ?itemOperated ?parts   
        (GROUP_CONCAT(DISTINCT ?itemOperatedTypeLab;
                SEPARATOR = ", ") AS ?itemOperatedTypeLabelConcat) 
        ?partsLabel 
        (GROUP_CONCAT(DISTINCT ?partTypeLab;
                SEPARATOR = ", ") AS ?partTypeLabelConcat) 
        WHERE {
            ?org wdt:P463 wd:Q85541788.
            ?org wdt:P121/wdt:P527* ?itemOperated .
            ?itemOperated wdt:P31 ?itemOperatedType.
            ?itemOperatedType rdfs:label ?itemOperatedTypeLab .
            OPTIONAL {
                ?itemOperated wdt:P625 ?geo .
            }
            FILTER(LANG(?itemOperatedTypeLab) = "en" )
            OPTIONAL {
                ?itemOperated wdt:P18 ?image.
            }
            OPTIONAL {
                ?itemOperated wdt:P6375 ?street_address .
            }
            OPTIONAL {
                ?org wdt:P121 ?itemOperated.
                ?itemOperated wdt:P527 ?parts.
                ?parts wdt:P31 ?partType.
                ?partType rdfs:label ?partTypeLab.
                FILTER(LANG(?partTypeLab) = "en")
            }
            SERVICE wikibase:label {
                bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en,fr".
            }
        }
        GROUP BY ?org ?orgLabel ?itemOperatedLabel ?geo ?itemOperated ?parts ?partsLabel 
    }
    BIND (URI(CONCAT(str(?org),"-ITEM_OPERATED_P121-",str(?itemOperated))) as ?role)
}
# SELECT ?org ?orgLabel ?itemOperatedLabel ?geo ?street_address_sample  ?image_sample ?itemOperated ?parts ?itemOperatedTypeLabelConcat ?partsLabel  ?partTypeLabelConcat

# This SPARQL finds all capacoa members in Wikidata and loads the venues that are operated by the organizations, including halls that are part of a building.
# You MUST drop the graph <http://wikidata.org/capacoa-members> before running this SPARQL otherwise concatenated nodes may accumulate if the concatenation order changes by chance.
# TODO: also get venues that organization "uses P2283", include fr labels.