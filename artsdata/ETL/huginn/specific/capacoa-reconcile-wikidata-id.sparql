PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
insert {
    graph ?g {
        ?org  schema:identifier  ?wd_org_Q
    }
}
where {
    values ?g {
        <graph_name_placeholder>  
    }
    graph ?g {
        ?org a schema:Organization ;
             schema:name ?name .
        OPTIONAL {
            ?org  schema:legalName ?legal_name  
        }
        OPTIONAL {
            ?org         schema:alternateName ?alternate_name 
        }
    }
    service <https://query.wikidata.org/sparql>
    {
        select distinct ?wd_org ?wd_orgLabel
        where {
            ?wd_org wdt:P31/wdt:P279* wd:Q43229 ;
                                    wdt:P463 wd:Q85541788  .
            SERVICE wikibase:label {
                bd:serviceParam wikibase:language "en,fr".
            } 
        } 
    }
    filter (str(?name) = str(?wd_orgLabel) || str(?legal_name) = str(?wd_orgLabel) || str(?alternate_name) = str(?wd_orgLabel))
    bind(STRAFTER(str(?wd_org),"wikidata.org/entity/")  as ?wd_org_Q)
}