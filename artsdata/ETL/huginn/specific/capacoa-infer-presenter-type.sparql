PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX capacoa:<https://capacoa.ca/vocabulary#>
insert {
    graph <http://huginn.culturecreates.com/capacoa-members> {
         capacoa:PresenterType a skos:Collection ;
                              skos:prefLabel "Presenter Type" ;
                              skos:member capacoa:PresenterType_Specialized ;
        					  skos:member capacoa:PresenterType_Multidisciplinary .
        ?org schema:additionalType capacoa:PresenterType_Specialized .
        capacoa:PresenterType_Specialized a skos:Concept ;
                                          skos:inScheme capacoa:InferredTypes .
        
        ?orgmulti  schema:additionalType capacoa:PresenterType_Multidisciplinary .
        capacoa:PresenterType_Multidisciplinary a skos:Concept ;
                                                skos:inScheme capacoa:InferredTypes .
       
    }
}
where {
    {
        select ?org 
        where {
            ?org  schema:additionalType capacoa:PrimaryActivity_pres .
            OPTIONAL {
                ?org schema:additionalType ?discipline .
                ?discipline ^skos:member capacoa:DisciplineGenre .
            }
            filter not exists {
                ?org schema:additionalType capacoa:PresentingFormat_occasional 
            }
        } 
        group by ?org
        having (count(?discipline) = 1)
    }
    UNION
    {
        select ?orgmulti 
        where {
            ?orgmulti  schema:additionalType capacoa:PrimaryActivity_pres .
            OPTIONAL {
                ?orgmulti schema:additionalType ?discipline .
                ?discipline ^skos:member capacoa:DisciplineGenre .
            }
            filter not exists {
                ?orgmulti schema:additionalType capacoa:PresentingFormat_occasional 
            }
        } 
        group by ?orgmulti
        having (count(?discipline) > 1)
    }
}