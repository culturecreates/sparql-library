PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX capacoa:<https://capacoa.ca/vocabulary/questionnaire#>
insert {
    graph <http://huginn.culturecreates.com/capacoa-members> {
         capacoa:PresenterType a skos:Collection ;
                              skos:prefLabel "Presenter Type" ;
                              skos:member capacoa:PresenterType_Specialized ;
        					  skos:member capacoa:PresenterType_Multidisciplinary .
        ?org schema:additionalType capacoa:PresenterType_Specialized .
        capacoa:PresenterType_Specialized a skos:Concept ;
                                          skos:inScheme capacoa:InferredTypes ;
                                          skos:prefLabel "Specialized" .
        
        ?orgmulti  schema:additionalType capacoa:PresenterType_Multidisciplinary .
        capacoa:PresenterType_Multidisciplinary a skos:Concept ;
                                                skos:inScheme capacoa:InferredTypes ;
                                                skos:prefLabel "Multidisciplinary" .
       
    }
}
where {
    {
        select ?org 
        where {
            ?org  schema:additionalType capacoa:PrimaryActivity_Presenting%2Fpromoting%20live%20performances%20and%2For%20operating%20venues .
            OPTIONAL {
                ?org schema:additionalType ?discipline .
                ?discipline ^skos:member capacoa:DisciplineGenre .
            }
            filter not exists {
                ?org schema:additionalType capacoa:PresentingFormat_Occasional%20presentations%20%2F%20one-offs 
            }
        } 
        group by ?org
        having (count(?discipline) = 1)
    }
    UNION
    {
        select ?orgmulti 
        where {
            ?orgmulti  schema:additionalType capacoa:PrimaryActivity_Presenting%2Fpromoting%20live%20performances%20and%2For%20operating%20venues .
            OPTIONAL {
                ?orgmulti schema:additionalType ?discipline .
                ?discipline ^skos:member capacoa:DisciplineGenre .
            }
            filter not exists {
                ?orgmulti schema:additionalType capacoa:PresentingFormat_Occasional%20presentations%20%2F%20one-offs 
            }
        } 
        group by ?orgmulti
        having (count(?discipline) > 1)
    }
}