PREFIX schema: <http://schema.org/>
insert {
    graph ?g {
        ?org  schema:sameAs  ?ad_org
    }
}
where {
    values ?g {
        <graph_name_placeholder>  
    }
    graph ?g {
        ?org a schema:Organization ;
             schema:name ?name .
        OPTIONAL {
            ?org  schema:alternateName ?alternate_name 
        }
    }
    service <http://db.artsdata.ca/repositories/artsdata> 
    {
        ?ad_org a schema:Organization ;
                schema:name ?ad_name .
        OPTIONAL {
            ?ad_org  schema:alternateName ?ad_alternate_name 
        }
        filter(regex(str(?ad_org),"K\\d+-\\d+"))
    }
    filter (str(?name) = str(?ad_name) || str(?name) = str(?ad_alternate_name) || str(?alternate_name ) = str(?ad_name) || str(?alternate_name) = str(?ad_alternate_name))
}