# This SPARQL collapses repeating Place and Organization blanknode addresses, contactPoints, geo an logos into a single blanknode
# Each entity per graph will have only a single nested blanknode
# In the rare case a property is different it will accumulate (2 values for the same property)

PREFIX schema: <http://schema.org/>
delete {
   graph ?g {
       ?entity ?prop ?bnode .
   }
}
insert {
   graph ?g {
       ?entity ?prop ?prime_node .
   }
}

where {
   
    graph ?g {
        ?entity  ?prop ?bnode .
        filter (isBLANK(?bnode)) 
    }
   
    { # select a random blank node to use as prime
        select ?entity ?g ?prop (sample(?node) as ?prime_node) where {
            graph ?g {
                values ?g {<GRAPH_PLACEHOLDER>}
                values ?entity_type {schema:Organization schema:Place }
                 values ?prop {schema:address schema:contactPoint schema:geo schema:logo}
                ?entity a ?entity_type ; # move out of graph to use inference.
                       ?prop ?node .
                filter (isBLANK(?node)) 
            }
        } group by ?entity ?g ?prop
    }
}