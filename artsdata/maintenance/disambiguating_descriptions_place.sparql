# This SPARQL adds a disambiguation description for Artsdata minted Places 
# into an Artsdata graph called <http://kg.artsdata.ca/disambiguatingDescriptions>
# Each import from Artsdata bus should run this SPARQL
PREFIX schema: <http://schema.org/>
delete {
    graph <http://kg.artsdata.ca/disambiguatingDescriptions> {
        ?candidate_adid schema:disambiguatingDescription ?previous
    }
}
insert {
    graph <http://kg.artsdata.ca/disambiguatingDescriptions> {
        ?candidate_adid schema:disambiguatingDescription ?description
    }
}
where {
    select ?s ?candidate_adid (sample(?disambiguatingDescription) as ?description)
    where {
        ?s a schema:Place ;
           schema:address ?address .
        OPTIONAL {
            ?s schema:disambiguatingDescription ?previous .
        }
        ?address a schema:PostalAddress ;
                 schema:addressLocality ?locality ;
                 schema:addressRegion ?region ;
                 schema:addressCountry ?country .
        OPTIONAL {
            ?s schema:sameAs ?adid .
            filter(contains(str(?adid), "kg.artsdata.ca/resource/"))
        }
        bind (concat(?locality," (",?region,") ",?country) as ?disambiguatingDescription )
        bind(coalesce(?adid,?s) as ?candidate_adid)
        filter(contains(str(?candidate_adid),"kg.artsdata.ca/resource/"))
    } group by ?s ?candidate_adid
}