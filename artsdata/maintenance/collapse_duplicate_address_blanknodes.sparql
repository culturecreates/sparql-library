# This SPARQL collapses repeating blanknode addresses
# Each place per graph will have only a single address blanknode
# In the rare case a property is different it will accumulate (2 values for the same property)

PREFIX schema: <http://schema.org/>
delete {
   graph ?g {
       ?entity schema:address ?bnode .
   }
}
insert {
   graph ?g {
       ?entity schema:address ?prime_node .
   }
}

where {
    graph ?g {
        ?entity   schema:address ?bnode .
        filter (isBLANK(?bnode)) 
    }
   
    { # select a random blank node to use as prime
        select ?entity ?g (sample(?node) as ?prime_node) where {
            graph ?g {
                # values ?g {<GRAPH_PLACEHOLDER>}
                values ?entity_type {schema:Organization schema:Place}
                ?entity a schema:Organization ; # move out of graph to use inference.
                       schema:address ?node .
                filter (isBLANK(?node)) 
            }
        } group by ?entity ?g
    }
}