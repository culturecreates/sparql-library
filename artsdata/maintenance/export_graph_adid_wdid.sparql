# This SPARQL can be used to dump a table of Artsdata ID for a given graph when asked by a 3rd party
# Graph includes Wikidata IDs and the graphs internal IDs

PREFIX schema: <http://schema.org/>
select ?internal_uri ?ad_uri ?wd_uri
where {
    {
        select * where {
            graph <http://kg.artsdata.ca/culture-creates/huginn/scenepro-orgs> {
                values ?type {
                    schema:Place schema:Organization
                }
                ?internal_uri a ?type .
            }
        }
    }
    ?internal_uri schema:sameAs ?ad_uri .
    filter(contains(str(?ad_uri),"artsdata.ca/resource/K"))
    optional {
        ?ad_uri schema:sameAs ?wdid .
        filter(contains(str(?wdid),"wikidata.org/entity/"))
    }
    bind(URI(?wdid) as ?wd_uri)
}