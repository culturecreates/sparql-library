# This SPARQL adds a disambiguation description for Artsdata minted Organizations 
# into an Artsdata graph called <http://kg.artsdata.ca/disambiguatingDescriptions>
# Each import from Artsdata bus should run this SPARQL
PREFIX schema: <http://schema.org/>
delete {
    graph <http://kg.artsdata.ca/disambiguatingDescriptions> {
        ?candidate_adid schema:disambiguatingDescription ?previous .
    }
}
insert {
    graph <http://kg.artsdata.ca/disambiguatingDescriptions> {
         ?candidate_adid schema:disambiguatingDescription ?description .
         << ?candidate_adid schema:disambiguatingDescription ?description >> prov:wasGeneratedBy [
        
            prov:startedAtTime ?startTime;
            prov:used adr:K1-13
         ] .
    }
}

where {
    select ?s ?candidate_adid (sample(?disambiguatingDescription) as ?description)
    where {
       
        ?s a schema:Organization .
        filter(isURI(?s))
        filter(!isBlank(?s))
        
        ?s  schema:address ?address .
        ?address a schema:PostalAddress ;
                 schema:addressLocality ?locality ;
                 schema:addressRegion ?region ;
                 schema:addressCountry ?country .
        OPTIONAL {
            ?s schema:disambiguatingDescription ?previous .
        }

        OPTIONAL {
            ?s schema:sameAs ?adid .
            filter(contains(str(?adid), "kg.artsdata.ca/resource/"))
        }
        bind(now() as ?startTime)
       
        bind (concat(?locality," (",?region,") ",?country) as ?disambiguatingDescription )
        bind(coalesce(?adid,?s) as ?candidate_adid)
        filter(contains(str(?candidate_adid),"kg.artsdata.ca/resource/"))
    } group by ?s ?candidate_adid
}