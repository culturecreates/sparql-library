# This SPARQL determines the disambiguating description for People using hasOccupation
# When more than one occupation exists, it will count them and use the one with the highest count (to avoid false occupation claims)
# If the count is equal it will use both by concatenating them into a string
# If the occupation is a URI then it will lookup the label.
# If there is a prefLable it will use it instead of the other labels (usefull for UDA with gendered occupations).
PREFIX schema: <http://schema.org/>
PREFIX adr: <http://kg.artsdata.ca/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
delete {
    graph <http://kg.artsdata.ca/disambiguatingDescriptions> {
        ?s schema:disambiguatingDescription ?previous
    }
}
insert {
    graph <http://kg.artsdata.ca/disambiguatingDescriptions> {
        ?s schema:disambiguatingDescription ?groupedfinal
    }
}

where {
    select ?s   ?previous (GROUP_CONCAT( distinct ?final ;
            SEPARATOR = ", ") as ?groupedfinal) 
    where {
        # This section counts identical occupations by source
        {
            select ?s  ?disambiguatingDescription (count(?disambiguatingDescription) as ?count) 
            where {
                # Group 1 uses ADIDs
                {
                    ?s a schema:Person .
                    filter(contains(str(?s),"kg.artsdata.ca/resource/K"))
                    graph ?g {
                        ?s schema:hasOccupation ?disambiguatingDescription .
                    }
                }
                UNION
                # Group 2 uses external IDs as object of sameAs ADID
                {
                    ?external_s a schema:Person .
                    ?s schema:sameAs ?external_s .
                    filter(contains(str(?s),"kg.artsdata.ca/resource/K"))
                    graph ?g {
                        ?external_s schema:hasOccupation ?disambiguatingDescription .
                    } 
                }
                UNION
                # Group 3 uses external IDs as subject of sameAs ADID
                {
                    ?external_s a schema:Person ;
                                schema:sameAs ?s .
                    filter(contains(str(?s),"kg.artsdata.ca/resource/K"))
                    graph ?g {
                        ?external_s schema:hasOccupation ?disambiguatingDescription .
                    } 
                }
            }
            group by ?s   ?disambiguatingDescription
        }
        .
        # This section selects the most popular occupations for each person
        {
            select ?s (max(?count2) as ?maxcount)
            {
                select ?s  ?disambiguatingDescription (count(?disambiguatingDescription) as ?count2) 
                where {
                    {
                        ?s a schema:Person .
                        filter(contains(str(?s),"kg.artsdata.ca/resource/K"))
                        graph ?g {
                            ?s schema:hasOccupation ?disambiguatingDescription .
                        }
                    }
                    UNION
                    {
                        ?external_s a schema:Person .
                        ?s schema:sameAs ?external_s .
                        filter(contains(str(?s),"kg.artsdata.ca/resource/K"))
                        graph ?g {
                            ?external_s schema:hasOccupation ?disambiguatingDescription .
                        }  
                    }
                    UNION
                    {
                        ?external_s a schema:Person ;
                                    schema:sameAs ?s .
                        filter(contains(str(?s),"kg.artsdata.ca/resource/K"))
                        graph ?g {
                            ?external_s schema:hasOccupation ?disambiguatingDescription .
                        } 
                    }
                }
                group by ?s ?disambiguatingDescription
            }
            group by ?s
        }
        # This filter keeps only the most popular occupation(s)
        filter (?maxcount = ?count)
        # This section looks up occupations that are URIs
        OPTIONAL {
            ?disambiguatingDescription rdfs:label ?label 
        }
        OPTIONAL {
            ?disambiguatingDescription skos:prefLabel ?prefLabel 
        }
        bind (coalesce(?prefLabel, ?label, ?disambiguatingDescription )as ?final)
        # Load previous so they can be deleted
        OPTIONAL {
            graph <http://kg.artsdata.ca/disambiguatingDescriptions> {
                ?s schema:disambiguatingDescription ?previous .
            }
        }
    } group by ?s   ?previous 
}